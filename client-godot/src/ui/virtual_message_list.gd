extends ScrollContainer
signal visible_messages(ids: Array[String])
signal interacted
signal audio_action(id: String, action: String)
signal image_opened(texture: Texture2D)
signal image_action(id: String, action: String)
signal retry_requested(id: String)
const Bubble = preload("res://scenes/ui/message_bubble.tscn")
const ChatTime = preload("res://src/ui/chat_time.gd")
const UiMotion = preload("res://src/ui/ui_motion.gd")
@onready var _canvas: Control = %Canvas
var _messages: Array[Dictionary] = []
var _offsets: Array[float] = []
var _heights: Dictionary = {}
var _nodes: Dictionary = {}
var _indices: Dictionary = {}
var _time_markers: Dictionary = {}
var _total := 0.0
var _width := 0.0
var _laying := false
var _visible: Array[String] = []
var _pending_fresh: Array[String] = []
var _restore_pending := false
var _restore_queued := false
var _pending_anchor: Dictionary = {}
var _pending_follow := false
var _scroll_tween: Tween = null
var _ui_style: RefCounted
var _smooth_follow := false
var _typing: Control
const TypingScene = preload("res://scenes/ui/typing_indicator.tscn")

func set_ui_style(style: RefCounted) -> void:
	_ui_style = style
	for bubble in _nodes.values(): bubble.set_ui_style(style)

func _ready() -> void:
	_typing = TypingScene.instantiate()
	# Keep the transient decoration out of public message-child enumeration.
	_canvas.add_child(_typing, false, Node.INTERNAL_MODE_BACK)
	get_v_scroll_bar().value_changed.connect(func(_value):
		if not _laying and not _restore_pending:
			_render())
	get_v_scroll_bar().gui_input.connect(_user_input)
	gui_input.connect(_user_input)
	resized.connect(_resized)

func set_messages(messages: Array[Dictionary], fresh_ids: Array[String] = []) -> void:
	var follow := is_at_latest()
	var anchor := get_reading_anchor()
	_messages = messages
	var next_markers := ChatTime.markers(messages)
	for id in _heights.keys():
		if _time_markers.get(id, "") != next_markers.get(id, ""): _heights.erase(id)
	_time_markers = next_markers
	_indices.clear()
	for index in messages.size():
		_indices[messages[index].id] = index
	for id in _nodes.keys():
		if not _indices.has(id):
			_remove(id)
	for id in _heights.keys():
		if not _indices.has(id):
			_heights.erase(id)
	_pending_fresh = fresh_ids.duplicate()
	_layout(anchor,follow)

func scroll_to_message(id: String) -> bool:
	_cancel_scroll()
	if not _indices.has(id):
		return false
	_layout({"id":id,"offset":0.0},false)
	return true

func scroll_to_latest() -> void:
	_cancel_scroll()
	_layout({},true)

## 平滑滚动到底部（用户点击「回到最新」时调用），滚动期间不打断布局。
func smooth_scroll_to_latest() -> void:
	if _messages.is_empty(): return
	_smooth_follow = true
	_retarget_scroll()

func _retarget_scroll() -> void:
	var target := maxi(0, roundi(_total - size.y))
	var distance := absf(float(scroll_vertical) - target)
	if distance < 2:
		_cancel_scroll()
		return
	var duration := clampf(distance / 2400.0, 0.15, 0.45)
	_scroll_tween = UiMotion.property_to(self, "scroll_vertical", target, duration, "scroll")
	_scroll_tween.finished.connect(func(): _smooth_follow = false; _scroll_tween = null)

func _cancel_scroll() -> void:
	UiMotion.cancel(self, "scroll", false)
	_scroll_tween = null
	_smooth_follow = false

func set_typing(active: bool) -> void:
	if _typing.visible == active: return
	var anchor := get_reading_anchor()
	var follow := is_at_latest()
	_typing.visible = active
	_layout(anchor, follow)

func get_visible_ids() -> Array[String]:
	return _visible.duplicate()

func get_reading_anchor() -> Dictionary:
	if _messages.is_empty() or _offsets.is_empty():
		return {"id":"","offset":0.0}
	if _restore_pending and not _pending_follow and _indices.has(_pending_anchor.get("id","")):
		return _pending_anchor.duplicate()
	if _restore_pending and _pending_follow:
		var target := maxf(0,_total-size.y)
		var target_index := _at(target)
		return {"id":_messages[target_index].id,"offset":target-_offsets[target_index]}
	var index := _at(float(scroll_vertical))
	return {"id":_messages[index].id,"offset":float(scroll_vertical)-_offsets[index]}

func is_at_latest() -> bool:
	if _messages.is_empty(): return true
	if _restore_pending: return _pending_follow
	return float(scroll_vertical) >= _total-size.y-24

func is_scrolling_to_latest() -> bool:
	return _smooth_follow

func set_audio_state(id: String, state: Dictionary) -> void:
	if _nodes.has(id):
		_nodes[id].set_audio_state(state)

func set_image_state(id: String, state: Dictionary) -> void:
	if _nodes.has(id):
		_nodes[id].set_image_state(state)

func _user_input(event: InputEvent) -> void:
	if (event is InputEventMouseButton and event.pressed) or event is InputEventPanGesture or (event is InputEventKey and event.pressed):
		_cancel_scroll()
		_restore_pending = false
		interacted.emit()

func _input(event: InputEvent) -> void:
	if event is InputEventMouseButton and event.pressed and event.button_index in [MOUSE_BUTTON_WHEEL_UP,MOUSE_BUTTON_WHEEL_DOWN] and get_global_rect().has_point(get_global_mouse_position()):
		_cancel_scroll()
		_restore_pending = false
		interacted.emit()

func _resized() -> void:
	var anchor := get_reading_anchor()
	var follow := is_at_latest()
	if absf(_width-size.x)>1:
		_heights.clear()
		_width = size.x
	_layout(anchor,follow)

func _process(_delta: float) -> void:
	if _laying or _messages.is_empty():
		return
	var anchor := get_reading_anchor()
	var follow := is_at_latest()
	var changed := false
	for id in _nodes:
		var system_message: bool = _nodes[id].get_node("%System").visible
		var minimum_height := 18.0 if system_message else 54.0
		var spacing := 4.0 if system_message else 17.0
		var height: float = maxf(minimum_height,_nodes[id].get_combined_minimum_size().y)+spacing
		if absf(float(_heights.get(id,100))-height)>1:
			_heights[id] = height
			changed = true
	if changed:
		_layout(anchor,follow)

func _layout(anchor: Dictionary, follow: bool) -> void:
	if not is_inside_tree():
		return
	_laying = true
	_pending_anchor = anchor.duplicate()
	_pending_follow = follow
	_restore_pending = true
	_offsets.clear()
	_total = 0
	for message in _messages:
		_offsets.append(_total)
		_total += float(_heights.get(message.id,100))
	_typing.position = Vector2(0, _total)
	_typing.size = Vector2(maxf(1, size.x-16), 48)
	if _typing.visible: _total += 68
	_canvas.custom_minimum_size = Vector2(0,_total)
	_canvas.size = Vector2(size.x,_total)
	var bar := get_v_scroll_bar()
	bar.max_value = maxf(_total,size.y)
	bar.page = size.y
	if follow and not _smooth_follow:
		scroll_vertical = maxi(0,roundi(_total-size.y))
	elif not _smooth_follow and _indices.has(anchor.get("id","")):
		scroll_vertical = roundi(_offsets[_indices[anchor.id]]+anchor.offset)
	_laying = false
	_render()
	if not _restore_queued:
		_restore_queued = true
		_defer_scroll_restore.call_deferred()

func _defer_scroll_restore() -> void:
	# A changed canvas minimum queues a parent ScrollContainer sort. Restore
	# after that sort, which can otherwise clamp to the old scroll range.
	_finish_scroll_restore.call_deferred()

func _finish_scroll_restore() -> void:
	_restore_queued = false
	if not _restore_pending or not is_inside_tree(): return
	_laying = true
	var bar := get_v_scroll_bar()
	bar.max_value = maxf(_total,size.y)
	bar.page = size.y
	if _pending_follow and not _smooth_follow:
		scroll_vertical = maxi(0,roundi(_total-size.y))
	elif not _smooth_follow and _indices.has(_pending_anchor.get("id","")):
		scroll_vertical = roundi(_offsets[_indices[_pending_anchor.id]]+_pending_anchor.offset)
	_restore_pending = false
	_laying = false
	if _smooth_follow: _retarget_scroll()
	_render()

func _render() -> void:
	var wanted: Dictionary = {}
	var visible: Array[String] = []
	if not _messages.is_empty():
		var first := maxi(0,_at(float(scroll_vertical))-2)
		var last := mini(_messages.size()-1,_at(float(scroll_vertical)+size.y)+2)
		for index in range(first,last+1):
			var message: Dictionary = _messages[index]
			var id: String = message.id
			wanted[id] = true
			if not _nodes.has(id):
				_create_bubble(id, message)
			else:
				_nodes[id].update_message(message)
			_nodes[id].set_time_marker(_time_markers.get(id, ""))
			var spacing := 4.0 if message.role == "system" else 17.0
			var height := float(_heights.get(id,100))-spacing
			_nodes[id].position = Vector2(0,_offsets[index])
			_nodes[id].size = Vector2(maxf(1,size.x-16),height)
			if _offsets[index]+height > scroll_vertical and _offsets[index]<scroll_vertical+size.y:
				visible.append(id)
	_pending_fresh.clear()
	for id in _nodes.keys():
		if not wanted.has(id):
			_remove(id)
	if visible != _visible:
		_visible = visible
		visible_messages.emit(get_visible_ids())
func _remove(id: String) -> void:
	var node: Node = _nodes[id]
	_canvas.remove_child(node)
	node.queue_free()
	_nodes.erase(id)

func _at(position_y: float) -> int:
	var low := 0
	var high := _offsets.size()-1
	while low < high:
		var middle := (low+high+1)/2
		if _offsets[middle] <= position_y:
			low = middle
		else:
			high = middle-1
	return low

func _create_bubble(id: String, message: Dictionary) -> void:
	var bubble = Bubble.instantiate()
	if _ui_style != null: bubble.set_ui_style(_ui_style)
	_canvas.add_child(bubble)
	bubble.configure(message)
	bubble.audio_action.connect(func(action): audio_action.emit(id,action))
	bubble.image_opened.connect(func(texture): image_opened.emit(texture))
	bubble.image_action.connect(func(action): image_action.emit(id,action))
	bubble.retry_requested.connect(func(): retry_requested.emit(id))
	_nodes[id] = bubble
	if _pending_fresh.has(id):
		_pending_fresh.erase(id)
		bubble.modulate.a = 0.0
		UiMotion.fade_in(bubble, UiMotion.DUR_BASE)








func _exit_tree() -> void:
	_cancel_scroll()
